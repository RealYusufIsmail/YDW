/*
 * Copyright 2022 Yusuf Arfan Ismail and other YDW contributors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at:
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java'
    id "com.diffplug.spotless" version "6.9.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

ext {
    dev_id = 'yusuf'
    dev_name = 'Yusuf Ismail'
    dev_email = 'yusufgamer222@gmail.com'
    dev_organization = 'RealYusufIsmail'
    dev_organizationUrl = 'https://github.com/RealYusufIsmail'

    gpl_name = 'GNU Library General Public License version 3'
    gpl_url = 'https://github.com/RealYusufIsmail/YDW/blob/master/LICENSE'

    // Make sure we have a default for initial configuration evaluation
    isReleaseVersion = false
}

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'jacoco' // code coverage reports

archivesBaseName = "YDW"
description = "YDW (Yusuf's Discord Wrapper) My own Discord Wrapper"
group 'io.github.realyusufismail'
version '0.0.4'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    //config
    implementation group: 'io.github.yusufsdiscordbot', name: 'config', version: '1.0.4'
    //https and ws
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '5.0.0-alpha.10'
    implementation group: 'com.neovisionaries', name: 'nv-websocket-client', version: '2.14'
    //json
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.3'
    //Logger
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.11'
    //google
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    //better time
    implementation group: 'joda-time', name: 'joda-time', version: '2.10.14'
    //Better mapping
    implementation group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        html.enabled true
    }
    jacocoTestReport.dependsOn test
    jacocoTestCoverageVerification.dependsOn jacocoTestReport
}

configurations.all {
    //noinspection GrUnresolvedAccess
    // ez vulnerability fix
    exclude group: "org.slf4j", module: "slf4j-log4j12"
}

compileJava {
    // Makes spotlessApply task run on every compile/build.
    dependsOn 'spotlessApply'

    // Nails the Java-Version of every Subproject
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

spotless {
    java {
        // Excludes build folder since it contains generated java classes.
        targetExclude("build/**")
        eclipse('4.21.0').configFile("${rootProject.rootDir}/meta/formatting/google-style-eclipse.xml")

        licenseHeader("""/*
 * Copyright 2022 Yusuf Arfan Ismail and other YDW contributors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at:
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */ """)
    }
}

javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO
    }

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

afterEvaluate { project ->
    ext.isReleaseVersion = !version.endsWith("RELEASE")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            //artifactId = project.artifactId // or maybe archiveBaseName?
            from components.java
            pom {
                name = project.archivesBaseName
                description = project.description
                url = "https://github.com/RealYusufIsmail/YDW"
                packaging = "jar"

                licenses {
                    license {
                        name = "${gpl_name}"
                        url = "${gpl_url}"
                    }
                }
                developers {
                    developer {
                        id = "${dev_id}"
                        name = "${dev_name}"
                        email = "${dev_email}"
                        organization = "${dev_organization}"
                        organizationUrl = "${dev_organizationUrl}"
                    }
                }
                scm {
                    connection = 'https://github.com/RealYusufIsmail/YDW.git'
                    developerConnection = 'scm:git:ssh://git@github.com/RealYusufIsmail/YDW.git'
                    url = 'github.com/YusufsDiscordbot/YDL'
                }
            }
        }
    }
    repositories {
        maven {
            afterEvaluate { project ->
                def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = (isReleaseVersion) ? releaseRepo : snapshotRepo

                //println "repos: " + version
                //println "repos: " + isReleaseVersion
                //println url

                credentials {
                    username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                    password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
                }
            }
        }
    }
}

signing {
    afterEvaluate { project ->
        //println "sign: " + version
        //println "sign: " + isReleaseVersion
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.mavenJava
    }
}
